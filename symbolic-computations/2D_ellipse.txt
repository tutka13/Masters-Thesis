/*elipsa uz so zmenenou bazou*/
ellipse: (1/(diff(m_1(t), t)^2 + diff(m_2(t), t)^2))*(((x - m_1(t))^2 * ((diff(m_1(t), t))^2/a^2 + (diff(m_2(t), t))^2/b^2))
+ (2 * (x - m_1(t)) * (y - m_2(t)) * diff(m_1(t), t) * diff(m_2(t), t) * (1/a^2 - 1/b^2))
+ ((y - m_2(t))^2 * ((diff(m_2(t), t))^2/a^2 + (diff(m_1(t), t))^2/b^2))) - 1$

/*koeficienty systemu*/
expr: expand(ellipse)$
A: ratsimp(coeff(expr, x, 2));
C: ratsimp(coeff(expr, y, 2));

x_0: coeff(expr, y, 0)$
y_0: coeff(expr, x, 0)$
x_1: coeff(expr, x, 1)$

B: ratsimp(coeff(x_1, y, 1));
D: ratsimp(coeff(x_0, x, 1));
E: ratsimp(coeff(y_0, y, 1));
F: ratsimp(coeff(x_0, x, 0));

/*M: matrix(
 [A,B/2,D/2], 
 [B/2,C,E/2], 
 [D/2,E/2,F]
)$*/

A1: ratsimp(diff(A,t,1)); 
B1: ratsimp(diff(B,t,1));
C1: ratsimp(diff(C,t,1));
D1: ratsimp(diff(D,t,1));
E1: ratsimp(diff(E,t,1));
F1: ratsimp(diff(F,t,1));

M_dot: matrix(
 [A1,B1/2,D1/2], 
 [B1/2,C1,E1/2], 
 [D1/2,E1/2,F1]
)$

/*vypocet invariantov*/
DELTA: ratsimp(determinant(M_dot));
is(DELTA=0);

DELTA_33: ratsimp(A1*C1 - B1^2/4);
is(DELTA_33=0);

DELTA_11: ratsimp(determinant(matrix(
 [A1,D1/2], 
 [D1/2,F1]
)))$

DELTA_22: ratsimp(determinant(matrix(
 [C1,E1/2], 
 [E1/2,F1]
)))$

T: DELTA_11 + DELTA_22;
is(T=[0]);

/*obalka vzdy existuje, pretoze jedna priamka vzdy pretina elipsu*/
conic_section: ratsimp(A1*x^2+B1*x*y+C1*y^2+D1*x+E1*y+F1)$
substituted_conic_section: ratsimp(subst([x = m_1(t), y = m_2(t)], conic_section))$
is(substituted_conic_section = 0);

/*chceme dostat koeficienty druhej priamky*/
alfa: ratsimp(A1/diff(m_1(t), t));
beta: ratsimp(C1/diff(m_2(t), t));
c: m_1(t)*diff(m_1(t), t)+m_2(t)*diff(m_2(t), t);
gama: ratsimp(-F1/c);

d: ratsimp(diff(m_1(t), t)*gama-c*alfa);
e: ratsimp(diff(m_2(t), t)*gama-c*beta);

gama_d: ratsimp((D1 + c*alfa)/diff(m_1(t),t));
gama_e: ratsimp((E1 + c*beta)/diff(m_2(t),t));

is(gama = gama_e);
is(gama = gama_d);